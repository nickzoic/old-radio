#!/usr/bin/perl -w
# $Id: plot3d,v 1.2 2010-01-12 03:27:07 nick Exp $

use strict;
use Data::Dumper;

my ($filename, $interval) = @ARGV;
$interval ||= 1_000_000;

open(my $fp, $filename) or die "can't open $filename";

$filename =~ s/\.log$//;
$filename =~ s/.*\///;

my $nodes = {};

my $lasttime = 0;
my $nexttime = 0;
my $count = 0;

while (defined($_ = <$fp>)) {
	my ($t, $nn, $op, $stat, $x, $y, $z, @ne) = split;
	next unless defined $t and $op eq 'Beacon';
	
	die "TIME OUT OF JOINT AT LINE $. TIME $t\n" if $t < $lasttime;
	$lasttime = $t;
	$nexttime ||= $t;
	
	while ($t > $nexttime) {
		makeplot(sprintf('wrl/%s-%06d.wrl', $filename, $count), $nodes);
		$nexttime += $interval;
		$count++;
	}
	$nodes->{$nn} = [ [ $x, $y, $z, $stat ], [ @ne ] ];
}
makeplot(sprintf('wrl/%s-%06d.wrl', $filename, $count), $nodes);

close($fp);

##############################################################################

sub makeplot {
	my ($filename, $nodes) = @_;

	print "Plotting $filename ... ", scalar keys %$nodes, " nodes\n";

 	open(my $fout, ">$filename");
	
	print $fout <<__EOF ;
#VRML V2.0 utf8
# plot3d

Background {
  groundColor [ 1 1 1 ]
  skyColor [ 1 1 1 ]
}

Viewpoint {
  position 0 0 1000
  orientation 0 0 1 0
}

DEF blackpaint Appearance {
  material Material {
    diffuseColor 0 0 0
    specularColor 0 0 0
  }
}

DEF whitepaint Appearance {
  material Material {
    diffuseColor 1 1 1
    specularColor 1 1 1
  }
}

DEF whiteball Shape {
#  appearance USE whitepaint
  appearance Appearance { } 
  geometry Sphere {
    radius 14
  }
}

DEF label FontStyle {
  size 18
  justify ["MIDDLE" "FIRST"]
  family ["SANS" "PLAIN"]
}

Shape {
  appearance USE blackpaint
  geometry IndexedLineSet {
    coord Coordinate {
      point [
__EOF

	my $nnum = 0;
	my %nnum;
	foreach my $n1 (sort { $a <=> $b } keys %$nodes) {
		my ($x, $y, $z) = @{$nodes->{$n1}[0]};
		print $fout "\t$x $y $z,\t# $nnum [$n1]\n";
		$nnum{$n1} = $nnum;
		$nnum++;
	}
	
	print $fout <<__EOF;
      ]
    }
    coordIndex [
__EOF

	foreach my $n1 (keys %$nodes) {
		my $nn1 = $nnum{$n1};
		next unless defined $nn1;
		
		my ($v1, $links) = @{$nodes->{$n1}};
		foreach my $n2 (@$links) {
			my $nn2 = $nnum{$n2};
			next unless defined $nn2;
			
			print $fout "\t$nn1, $nn2, -1,\t# [$n1 -> $n2]\n";	
		}
	}

	print $fout <<__EOF;
    ]
  }
}
__EOF

	foreach my $n1 (keys %$nodes) {
		my ($x, $y, $z, $stat) = @{$nodes->{$n1}[0]};
		print $fout <<__EOF;

Transform {  
  translation $x $y $z
  children [
    Billboard {
      children [
	Transform {
 	  translation 0 -7 15
	  children [
            Shape {
	      appearance USE blackpaint
              geometry Text {
    	        string [ "$n1" ]
	        fontStyle USE label
              }
	    }
          ]
        }
	USE whiteball
      ]
      axisOfRotation 0 0 0
    }
  ]
}

__EOF
	}
}
